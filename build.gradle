plugins {
    alias(libs.plugins.versioner)
    alias(libs.plugins.index.scan)
    alias(libs.plugins.owasp.dependencycheck)
    alias(libs.plugins.nexus.publish.plugin)
    alias libs.plugins.cyclonedx.bom apply false
    alias libs.plugins.licensee.plugin apply false
}

group = 'com.github.nagyesta.abort-mission'

apply from: "config/ossindex/ossIndexAudit.gradle"

project.ext {
    gitToken = project.hasProperty('githubToken') ? (project.property('githubToken') as String) : ''
    gitUser = project.hasProperty('githubUser') ? (project.property('githubUser') as String) : ''
    ossrhUser = project.hasProperty('ossrhUsername') ? (project.property('ossrhUsername') as String) : ''
    ossrhPass = project.hasProperty('ossrhPassword') ? (project.property('ossrhPassword') as String) : ''
    ossIndexUser = project.hasProperty('ossIndexUsername') ? (project.property('ossIndexUsername') as String) : ''
    ossIndexPass = project.hasProperty('ossIndexPassword') ? (project.property('ossIndexPassword') as String) : ''
    repoUrl = 'https://github.com/nagyesta/abort-mission'
    licenseName = 'MIT License'
    licenseUrl = 'https://raw.githubusercontent.com/nagyesta/abort-mission/main/LICENSE'
    maintainerId = 'nagyesta'
    maintainerName = 'Istvan Zoltan Nagy'
    maintainerUrl = 'https://github.com/nagyesta/'
    scmConnection = 'scm:git:https://github.com/nagyesta/abort-mission.git'
    scmProjectUrl = 'https://github.com/nagyesta/abort-mission/'
}

versioner {
    startFrom {
        major = 1
        minor = 1
        patch = 1
    }
    match {
        major = '{major}'
        minor = '{minor}'
        patch = '{patch}'
    }
    pattern {
        pattern = "%M.%m.%p"
    }
    git {
        authentication {
            https {
                token = project.ext.gitToken
            }
        }
    }
    tag {
        prefix = 'v'
        useCommitMessage = true
    }
}

configure(subprojects.findAll({
    !'boosters'.equalsIgnoreCase(it.name) && !'mission-report'.equalsIgnoreCase(it.name)
})) {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'org.sonatype.gradle.plugins.scan'
    //noinspection SpellCheckingInspection
    apply plugin: 'org.owasp.dependencycheck'
    apply plugin: 'org.cyclonedx.bom'
    apply plugin: 'app.cash.licensee'

    versioner.apply()

    version = rootProject.version
    //noinspection GroovyUnusedAssignment
    repositories {
        mavenCentral()
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
        withJavadocJar()
        withSourcesJar()
    }

    jacocoTestReport {
        reports {
            xml.required.set(true)
            xml.outputLocation.set(layout.buildDirectory.file("reports/jacoco/report.xml").get().getAsFile())
            csv.required.set(false)
            html.required.set(true)
            html.outputLocation.set(layout.buildDirectory.dir("reports/jacoco/html").get().getAsFile())
        }
    }

    test.finalizedBy jacocoTestReport
    jacocoTestReport.finalizedBy jacocoTestCoverageVerification

    jacoco {
        toolVersion = libs.versions.jacoco.get()
    }

    jacocoTestCoverageVerification {
        inputs.file(layout.buildDirectory.file("reports/jacoco/report.xml").get().getAsFile())
        outputs.file(layout.buildDirectory.file("reports/jacoco/jacocoTestCoverageVerification").get().getAsFile())

        violationRules {
            rule {
                limit {
                    counter = 'LINE'
                    //noinspection SpellCheckingInspection
                    value = 'COVEREDRATIO'
                    minimum = 0.8
                }
                limit {
                    counter = 'BRANCH'
                    //noinspection SpellCheckingInspection
                    value = 'COVEREDRATIO'
                    minimum = 0.8
                }
                excludes = [
                        "testkit",
                        "booster-junit4"
                ]
            }
            rule {
                element = 'CLASS'
                limit {
                    counter = 'LINE'
                    //noinspection SpellCheckingInspection
                    value = 'COVEREDRATIO'
                    minimum = 0.5
                }
                limit {
                    counter = 'BRANCH'
                    //noinspection SpellCheckingInspection
                    value = 'COVEREDRATIO'
                    minimum = 0.5
                }
                excludes = [
                        "com.github.nagyesta.abortmission.core.LaunchSequenceTemplate",
                        "com.github.nagyesta.abortmission.core.healthcheck.StatisticsLogger",
                        "com.github.nagyesta.abortmission.core.matcher.impl.CustomMatcher",
                        "com.github.nagyesta.abortmission.testkit.spring.StaticFireTestAssets",
                        "com.github.nagyesta.abortmission.testkit.vanilla.FuelTankTestAssets",
                        "com.github.nagyesta.abortmission.testkit.vanilla.ParachuteDropTestAssets",
                        "com.github.nagyesta.abortmission.testkit.NoOpMatcher",
                        "com.github.nagyesta.abortmission.booster.junit4.support.LaunchAbortTestWatcher.1",
                        "com.github.nagyesta.abortmission.reporting.AbortMissionFlightEvaluationReportApp",
                        "org.springframework.boot.loader.JarLauncher"
                ]
            }
        }
        doLast {
            layout.buildDirectory.file("reports/jacoco/jacocoTestCoverageVerification").get().getAsFile().write("Passed")
        }
    }
    jar.dependsOn check

    tasks.withType(Javadoc).configureEach {
        options.addBooleanOption("Xdoclint:-missing", true)
    }

    tasks.withType(Checkstyle).configureEach {
        configProperties = [base_dir: rootDir.toString(), cache_file: layout.buildDirectory.file("checkstyle/cacheFile").get().getAsFile()]
        reports {
            xml.required.set(false)
            html.required.set(true)
            html.stylesheet resources.text
                    .fromFile(rootProject.file('config/checkstyle/checkstyle-stylesheet.xsl') as String)
        }
    }
    checkstyle.toolVersion = libs.versions.checkstyle.get()

    cyclonedxBom {
        includeConfigs = ["runtimeClasspath"]
        skipConfigs = ["compileClasspath", "testCompileClasspath"]
        skipProjects = []
        projectType = "library"
        schemaVersion = "1.5"
        destination = file("build/reports")
        outputName = "bom"
        outputFormat = "json"
        //noinspection UnnecessaryQualifiedReference
        final def attachmentText = new org.cyclonedx.model.AttachmentText()
        attachmentText.setText(file("${project.rootProject.projectDir}/LICENSE").readBytes().encodeBase64().toString())
        attachmentText.setEncoding("base64")
        attachmentText.setContentType("text/plain")
        //noinspection UnnecessaryQualifiedReference
        final def license = new org.cyclonedx.model.License()
        license.setName(project.rootProject.ext.licenseName)
        license.setLicenseText(attachmentText)
        license.setUrl(project.rootProject.ext.licenseUrl)
        setLicenseChoice {
            it.addLicense(license)
        }
    }

    licensee {
        allow("Apache-2.0")
        allow("MIT")
        allow("EPL-1.0")
        allow("BSD-2-Clause")
        allow("BSD-3-Clause")
        allowUrl("https://opensource.org/licenses/MIT")
        allowUrl("http://www.opensource.org/licenses/mit-license")
        allowUrl("https://github.com/jquery/jquery/blob/master/MIT-LICENSE.txt")
        allowUrl("https://www.eclipse.org/legal/epl-v20.html")
        allowUrl("http://www.eclipse.org/legal/epl-2.0")
        allowUrl("http://www.eclipse.org/org/documents/edl-v10.php")
        allowUrl("https://asm.ow2.io/license.html")
        unusedAction("IGNORE")
    }

    tasks.register('copyLegalDocs', Copy.class) {
        from file("${project.rootProject.projectDir}/LICENSE")
        from layout.buildDirectory.file("reports/licensee/artifacts.json").get().asFile
        from layout.buildDirectory.file("reports/bom.json").get().asFile
        from layout.buildDirectory.file("nodejs-dependency-licenses.csv").get().asFile
        into layout.buildDirectory.dir("resources/main/META-INF").get().asFile
        rename('artifacts.json', 'dependency-licenses.json')
        rename('bom.json', 'SBOM.json')
    }
    //noinspection ConfigurationAvoidance
    if (project.name == "flight-evaluation-report") {
        tasks.copyLegalDocs.dependsOn("processTemplates")
    }
    tasks.copyLegalDocs.dependsOn(tasks.licensee)
    tasks.copyLegalDocs.dependsOn(tasks.cyclonedxBom)
    tasks.javadoc.dependsOn(tasks.copyLegalDocs)
    tasks.compileTestJava.dependsOn(tasks.copyLegalDocs)
    tasks.checkstyleMain.dependsOn(tasks.copyLegalDocs)
    tasks.processResources.finalizedBy(tasks.copyLegalDocs)

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/nagyesta/abort-mission")
                credentials {
                    username = rootProject.ext.gitUser
                    password = rootProject.ext.gitToken
                }
            }
        }
    }

    ossIndexAudit {
        username = rootProject.ext.ossIndexUser
        password = rootProject.ext.ossIndexPass
        printBanner = false
        colorEnabled = true
        showAll = false
        outputFormat = 'DEPENDENCY_GRAPH'
        excludeVulnerabilityIds = rootProject.ext.ossIndexExclusions
    }

    //Disable metadata publishing and rely on Maven only
    tasks.withType(GenerateModuleMetadata).configureEach {
        enabled = false
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = project.ext.ossrhUser
            password = project.ext.ossrhPass
        }
    }
}

ossIndexAudit {
    username = rootProject.ext.ossIndexUser
    password = rootProject.ext.ossIndexPass
    printBanner = false
    colorEnabled = true
    showAll = false
    outputFormat = 'DEPENDENCY_GRAPH'
    excludeVulnerabilityIds = rootProject.ext.ossIndexExclusions
}

repositories {
    mavenCentral()
}

