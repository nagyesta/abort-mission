plugins {
    alias(libs.plugins.versioner)
    alias(libs.plugins.index.scan)
    alias(libs.plugins.owasp.dependencycheck)
}

group = 'com.github.nagyesta.abort-mission'

project.ext {
    gitToken = project.hasProperty('githubToken') ? (project.property('githubToken') as String) : ''
    gitUser = project.hasProperty('githubUser') ? (project.property('githubUser') as String) : ''
    ossrhUser = project.hasProperty('ossrhUsername') ? (project.property('ossrhUsername') as String) : ''
    ossrhPass = project.hasProperty('ossrhPassword') ? (project.property('ossrhPassword') as String) : ''
    ossIndexUser = project.hasProperty('ossIndexUsername') ? (project.property('ossIndexUsername') as String) : ''
    ossIndexPass = project.hasProperty('ossIndexPassword') ? (project.property('ossIndexPassword') as String) : ''
    ossIndexExclusions = ["CVE-2016-1000027", "CVE-2018-14335", "sonatype-2020-1324"]
    repoUrl = 'https://github.com/nagyesta/abort-mission'
    licenseName = 'MIT License'
    licenseUrl = 'https://raw.githubusercontent.com/nagyesta/abort-mission/main/LICENSE'
    maintainerId = 'nagyesta'
    maintainerName = 'Istvan Zoltan Nagy'
    maintainerUrl = 'https://github.com/nagyesta/'
    scmConnection = 'scm:git:https://github.com/nagyesta/abort-mission.git'
    scmProjectUrl = 'https://github.com/nagyesta/abort-mission/'
}

versioner {
    startFrom {
        major = 1
        minor = 1
        patch = 1
    }
    match {
        major = '{major}'
        minor = '{minor}'
        patch = '{patch}'
    }
    pattern {
        pattern = "%M.%m.%p"
    }
    git {
        authentication {
            https {
                token = project.ext.gitToken
            }
        }
    }
    tag {
        prefix = 'v'
        useCommitMessage = true
    }
}

configure(subprojects.findAll({
    !'boosters'.equalsIgnoreCase(it.name) && !'strongback'.equalsIgnoreCase(it.name) && !'mission-report'.equalsIgnoreCase(it.name)
})) {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'org.sonatype.gradle.plugins.scan'
    //noinspection SpellCheckingInspection
    apply plugin: 'org.owasp.dependencycheck'

    versioner.apply()

    version = rootProject.version
    //noinspection GroovyUnusedAssignment
    sourceCompatibility = '11'
    repositories {
        mavenCentral {
            metadataSources {
                mavenPom()
                artifact()
                ignoreGradleMetadataRedirection()
            }
        }
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    jacocoTestReport {
        reports {
            xml.required.set(true)
            xml.destination file("$buildDir/reports/jacoco/report.xml")
            csv.required.set(false)
            html.required.set(true)
            html.destination file("$buildDir/reports/jacoco/html")
        }
    }

    test.finalizedBy jacocoTestReport
    jacocoTestReport.finalizedBy jacocoTestCoverageVerification

    jacoco {
        toolVersion = libs.versions.jacoco.get()
    }

    jacocoTestCoverageVerification {
        inputs.file(file("${buildDir}/reports/jacoco/report.xml") as String)
        outputs.file(file("${buildDir}/reports/jacoco/jacocoTestCoverageVerification") as String)

        violationRules {
            rule {
                limit {
                    counter = 'LINE'
                    //noinspection SpellCheckingInspection
                    value = 'COVEREDRATIO'
                    minimum = 0.8
                }
                limit {
                    counter = 'BRANCH'
                    //noinspection SpellCheckingInspection
                    value = 'COVEREDRATIO'
                    minimum = 0.8
                }
                excludes = [
                        "testkit",
                        "booster-junit4"
                ]
            }
            rule {
                element = 'CLASS'
                limit {
                    counter = 'LINE'
                    //noinspection SpellCheckingInspection
                    value = 'COVEREDRATIO'
                    minimum = 0.5
                }
                limit {
                    counter = 'BRANCH'
                    //noinspection SpellCheckingInspection
                    value = 'COVEREDRATIO'
                    minimum = 0.5
                }
                excludes = [
                        "com.github.nagyesta.abortmission.core.LaunchSequenceTemplate",
                        "com.github.nagyesta.abortmission.core.healthcheck.StatisticsLogger",
                        "com.github.nagyesta.abortmission.core.matcher.impl.CustomMatcher",
                        "com.github.nagyesta.abortmission.testkit.spring.StaticFireTestAssets",
                        "com.github.nagyesta.abortmission.testkit.vanilla.FuelTankTestAssets",
                        "com.github.nagyesta.abortmission.testkit.vanilla.ParachuteDropTestAssets",
                        "com.github.nagyesta.abortmission.testkit.NoOpMatcher",
                        "com.github.nagyesta.abortmission.booster.junit4.support.LaunchAbortTestWatcher.1",
                        "com.github.nagyesta.abortmission.reporting.AbortMissionFlightEvaluationReportApp"
                ]
            }
        }
        doLast {
            file("${buildDir}/reports/jacoco/jacocoTestCoverageVerification").write("Passed")
        }
    }
    jar.dependsOn check

    tasks.withType(Checkstyle) {
        configProperties = [base_dir: rootDir.toString(), cache_file: file("${buildDir}/checkstyle/cacheFile")]
        reports {
            xml.required.set(false)
            html.required.set(true)
            html.stylesheet resources.text
                    .fromFile(rootProject.file('config/checkstyle/checkstyle-stylesheet.xsl') as String)
        }
    }
    checkstyle.toolVersion = libs.versions.checkstyle.get()

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/nagyesta/abort-mission")
                credentials {
                    username = rootProject.ext.gitUser
                    password = rootProject.ext.gitToken
                }
            }
            maven {
                name = "ossrh"
                url = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2")
                credentials {
                    username = rootProject.ext.ossrhUser
                    password = rootProject.ext.ossrhPass
                }
            }
        }
    }

    ossIndexAudit {
        username = rootProject.ext.ossIndexUser
        password = rootProject.ext.ossIndexPass
        printBanner = false
        colorEnabled = true
        showAll = false
        dependencyGraph = true
        excludeVulnerabilityIds = rootProject.ext.ossIndexExclusions
    }

    //Disable metadata publishing and rely on Maven only
    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }
}

ossIndexAudit {
    username = rootProject.ext.ossIndexUser
    password = rootProject.ext.ossIndexPass
    printBanner = false
    colorEnabled = true
    showAll = false
    dependencyGraph = true
    excludeVulnerabilityIds = rootProject.ext.ossIndexExclusions
}

repositories {
    mavenCentral {
        metadataSources {
            mavenPom()
            artifact()
            ignoreGradleMetadataRedirection()
        }
    }
}

