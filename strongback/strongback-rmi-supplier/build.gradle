group = "${rootProject.group}.strongback"

project.ext {
    artifactDisplayName = "Abort Mission - Strongback RMI Supplier"
    artifactDescription = "RMI implementation of Strongback logic."
}

dependencies {
    implementation (project(":strongback:strongback-base")) {
        transitive = false
    }
    implementation project(":mission-control")
    implementation libs.slf4j.api
    testImplementation project(":boosters:testkit")
    testImplementation project(":boosters:booster-junit-jupiter")
    testImplementation libs.jupiter.core
    testImplementation libs.mockito.core
    testImplementation libs.spring.boot.starter
    testImplementation libs.spring.boot.starter.test
    testImplementation libs.jupiter.platform.testkit
    constraints {
        testImplementation libs.bundles.spring.test
    }
}

test {
    useJUnitPlatform {
        systemProperty("abort-mission.report.directory", file("${buildDir}/reports/abort-mission/"))
        includeTags 'unit', 'integration'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = "abort.${project.name}"
            pom {
                name = "${project.artifactDisplayName}"
                description = "${project.artifactDescription}"
                url = rootProject.ext.repoUrl
                licenses {
                    license {
                        name = rootProject.ext.licenseName
                        url = rootProject.ext.licenseUrl
                    }
                }
                developers {
                    developer {
                        id = rootProject.ext.maintainerId
                        name = rootProject.ext.maintainerName
                        url = rootProject.ext.maintainerUrl
                    }
                }
                scm {
                    connection = rootProject.ext.scmConnection
                    developerConnection = rootProject.ext.scmConnection
                    url = rootProject.ext.scmProjectUrl
                }
                withXml {
                    asNode().dependencies.'*'.findAll() {
                        it.scope.text() == 'runtime'
                    }.each { it.scope*.value = 'compile' }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
