group = "${rootProject.group}.boosters"

project.ext {
    artifactDisplayName = "Abort Mission - Cucumber JVM Booster"
    artifactDescription = "Booster module of Abort Mission adding Cucumber JVM bindings."
}

dependencies {
    implementation project(":mission-control")
    implementation libs.slf4j.api
    implementation libs.cucumber.java
    implementation libs.cucumber.junit
    testImplementation project(":boosters:testkit")
    testImplementation libs.spring.boot.starter
    testImplementation libs.spring.boot.starter.test
    testImplementation libs.cucumber.spring
    testImplementation libs.jupiter.core
    testImplementation libs.jupiter.vintage.engine
    testImplementation libs.jupiter.platform.testkit
}

test {
    useJUnitPlatform {
        systemProperty("abort-mission.report.directory", file("${buildDir}/reports/abort-mission/"))
        includeTags 'unit', 'integration'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = "abort.${project.name}"
            pom {
                name = "${project.artifactDisplayName}"
                description = "${project.artifactDescription}"
                url = rootProject.ext.repoUrl
                licenses {
                    license {
                        name = rootProject.ext.licenseName
                        url = rootProject.ext.licenseUrl
                    }
                }
                developers {
                    developer {
                        id = rootProject.ext.maintainerId
                        name = rootProject.ext.maintainerName
                        url = rootProject.ext.maintainerUrl
                    }
                }
                scm {
                    connection = rootProject.ext.scmConnection
                    developerConnection = rootProject.ext.scmConnection
                    url = rootProject.ext.scmProjectUrl
                }
                withXml {
                    asNode().dependencies.'*'.findAll() {
                        it.scope.text() == 'runtime'
                    }.each { it.scope*.value = 'compile' }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
