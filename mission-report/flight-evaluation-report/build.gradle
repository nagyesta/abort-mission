plugins {
    id 'org.springframework.boot' version '2.3.3.RELEASE'
    id 'java'
    //noinspection SpellCheckingInspection
    id "io.freefair.lombok" version "5.2.1"
    id "org.padler.gradle.minify" version "1.6.0"
    id "org.sonatype.gradle.plugins.scan" version "2.0.4"
}

group = "${rootProject.group}.reports"

project.ext {
    artifactDisplayName = "Abort Mission - Flight Evaluation Report"
    artifactDescription = "Core reporting module of Abort Mission generating reports from telemetry data."
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter:${rootProject.ext.springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-thymeleaf:${rootProject.ext.springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-json:${rootProject.ext.springBootVersion}"
    implementation "com.networknt:json-schema-validator:${rootProject.ext.jsonSchemaValidatorVersion}"
    annotationProcessor "org.projectlombok:lombok:${rootProject.ext.lombokVersion}"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:${rootProject.ext.springBootVersion}"
    testImplementation "org.springframework.boot:spring-boot-starter-test:${rootProject.ext.springBootVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter:${rootProject.ext.jupiterPlatformVersion}"
}

minification {
    cssSrcDir = file("src/main/templates/css")
    cssDstDir = file("${buildDir}/css")
    jsSrcDir = file("src/main/templates/js")
    jsDstDir = file("${buildDir}/js")

    createCssSourceMaps = false
    createJsSourceMaps = false

    js {
        compilationLevel = "WHITESPACE_ONLY"
    }
}

task combineCss {
    inputs.files(fileTree("${buildDir}/css", {
        include "**.min.css"
    })).skipWhenEmpty()
    outputs.file("${buildDir}/resources/main/templates/css/all.min.css")
    doLast {
        outputs.files.singleFile.withOutputStream { out ->
            def cssRoot = "${buildDir}/css"
            def includes = [
                    file("${cssRoot}/reset.min.css"),
                    file("${cssRoot}/base.min.css"),
                    file("${cssRoot}/colors.min.css"),
                    file("${cssRoot}/columns.min.css"),
                    file("${cssRoot}/rows.min.css"),
                    file("${cssRoot}/stats.min.css")
            ]
            for (file in includes) {
                file.withInputStream {
                    out << "/* ${file.name} */"
                    out << it << '\n'
                }
            }
        }
    }
    dependsOn minify
}

task combineJs {
    inputs.files(fileTree("${buildDir}/js", {
        include "**.min.js"
    })).skipWhenEmpty()
    outputs.file("${buildDir}/resources/main/templates/js/all.min.js")
    doLast {
        outputs.files.singleFile.withOutputStream { out ->
            def jsRoot = "${buildDir}/js"
            def includes = [
                    file("${jsRoot}/toggle.min.js")
            ]
            for (file in includes) {
                file.withInputStream {
                    out << "/* ${file.name} */"
                    out << it << '\n'
                }
            }
        }
    }
    dependsOn minify
}

task processTemplates {
    inputs.files(fileTree("src/main/templates/html")).skipWhenEmpty()
    outputs.dir("${buildDir}/resources/main/templates/html")
    doLast {
        for (template in inputs.files) {
            def outFile = file("${buildDir}/resources/main/templates/html/${template.name}")
            outFile.delete()
            template.eachLine { line ->
                outFile.append(line.replaceAll("<!-- [a-zA-Z0-9 ]+ -->", "").trim())
            }
        }
    }
    dependsOn combineCss
    dependsOn combineJs
}

processResources.finalizedBy processTemplates

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact bootJar
            artifactId = "abort.${project.name}"
            pom {
                name = "${project.artifactDisplayName}"
                description = "${project.artifactDescription}"
                url = rootProject.ext.repoUrl
                packaging = "jar"
                licenses {
                    license {
                        name = rootProject.ext.licenseName
                        url = rootProject.ext.licenseUrl
                    }
                }
                developers {
                    developer {
                        id = rootProject.ext.maintainerId
                        name = rootProject.ext.maintainerName
                        url = rootProject.ext.maintainerUrl
                    }
                }
                scm {
                    connection = rootProject.ext.scmConnection
                    developerConnection = rootProject.ext.scmConnection
                    url = rootProject.ext.scmProjectUrl
                }
                withXml {
                    asNode().dependencies.findAll() {
                        def parent = it.parent()
                        parent.remove(it)
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
